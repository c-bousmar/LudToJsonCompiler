(game "Quathaxx" 
    (players 2) 
    (equipment { 
        (board (quadhex 4)) 
        (piece "Marker" Each 
            (or 
                (move 
                    (from) 
                    (to (sites Around (from)) if:(is Empty (to))) 
                    copy:True
                )
                (move 
                    (from) 
                    (to 
                        (difference 
                            (expand origin:(from) steps:2) 
                            (expand origin:(from))
                        ) 
                        if:(is Empty (to))
                    )
                )
                (then
                    (forEach Site 
                        (intersection 
                            (sites Around (last To)) 
                            (sites Occupied by:Next)
                        )
                        (and 
                            (remove (site)) 
                            (add (piece (id "Marker" Mover)) (to (site))) 
                        )
                    )
                )
            )
        )
        }
    ) 
    (rules 
        (start {
            (set Score Each 2)
            (place "Marker1" {"L1" "L37" "AZ19"})
            (place "Marker2" {"A19" "AO37" "AO1"})
        })
        (play (forEach Piece
                (then 
                    (and 
                        (set Score P1 (count Sites in:(sites Occupied by:P1)))	 
                        (set Score P2 (count Sites in:(sites Occupied by:P2)))
                    )
                )	
            )
        )
        
        (end
            (if (or 
                    ("NoSites" (sites Occupied by:Next)) 
                    (is Full)
                )
                (byScore)
            )
        )
    )
)

//------------------------------------------------------------------------------

(metadata
    
    (info
        {
        (description "Quathaxx is a quadHex-tilling variant of Ataxx an arcade game invented in 1990 by the Leland Corporation and was originally called Infection.")
        (rules "Each player begins with three pieces on the six corners of the board. Players move one piece one or two spaces in any direction orthogonally or diagonally. If the piece is moved on space, another piece is added on the origin space. All of the opponent's pieces adjacent to the space to which the player moved now belong to the player who moved. Play ends when all spaces have been filled or one of the players has no remaining pieces. The player with the most pieces wins.")
        (id "4292")
        (version "1.3.14")
        (classification "board/space/territory")
        (author "Eric Piette")
        (credit "Eric Piette")
        (date "2024")
        }
    )
    
    (graphics {
        (player Colour P1 (colour Red))
        (player Colour P2 (colour Blue))
    })
    
)
