(game "Fianco" 
    (players {(player N) (player S)})
    (equipment { 
        (board (square 9))
        (piece "Marker" Each)
        (regions P1 (sites Top))
        (regions P2 (sites Bottom))
    }) 
    (rules 
        (start { 
            (place "Marker1" {"A1" "B1" "C1" "D1" "E1" "F1" "G1" "H1" "I1" "B2" "C3" "D4" "F4" "G3" "H2"})
            (place "Marker2" {"A9" "B9" "C9" "D9" "E9" "F9" "G9" "H9" "I9" "B8" "C7" "D6" "F6" "G7" "H8"})
        })
        (play 
            (priority {
                (forEach Piece "Marker" 
                    (move Hop
                        (directions {FL FR})
                        (between
                            if:("IsEnemyAt" (between))
                            (apply (remove (between)))
                        )
                        (to if:(is Empty (to)))
                    )
                )
                (forEach Piece "Marker" 
                    (move Step (directions {Forward Leftward Rightward})
                        (to if:(is Empty (to)))
                    )
                )
            })
        )
        (end ("ReachWin" (sites Mover) Mover))
    )
)

//------------------------------------------------------------------------------

(metadata
    (info
        {
        (description "In Fianco, which is Italian for \"side\" or \"flank\", you want to be the first player to move one of your pieces to the opponent's back line.")
        (rules "The game is played on a 9x9 square board. The pieces form an empty hourglass with a missing piece in the center. The pieces closest to a player's side are of their color. A stone can be moved forward or sideways to an adjacent empty cell. A piece can jump diagonally above an adjacent enemy to capture them. Captures are mandatory. A player wins if he places one of his stones on the last row.")
        (id "4276")
        (source "<a href=\"https://www.di.fc.ul.pt/~jpn/gv/fianco.htm\" target=\"_blank\" class=\"style1\" style=\"color: #0000EE\" />The World of Abstract Games</a>")
        (version "1.3.14")
        (classification "board/race/reach")
        (author "Fred Horn")
        (credit "Achille Morenville")
        (date "1987")
        }
    )
)
